cmake_minimum_required (VERSION 2.8)
project (tralloc C)


set (TRALLOC_VERSION "0.7.0")

set (TRALLOC_THREADS      true CACHE BOOL "threads feature")
set (TRALLOC_LENGTH       true CACHE BOOL "length feature")
set (TRALLOC_DESTRUCTOR   true CACHE BOOL "destructor feature")
set (TRALLOC_REFERENCE    true CACHE BOOL "reference feature")
set (TRALLOC_POOL         true CACHE BOOL "pool feature")
set (TRALLOC_UTILS_BUFFER true CACHE BOOL "buffer util")

set (TRALLOC_DEBUG_THREADS   true CACHE BOOL "threads in debug feature")
set (TRALLOC_DEBUG_CALLBACKS true CACHE BOOL "callbacks in debug feature")
set (TRALLOC_DEBUG_STATS     true CACHE BOOL "stats in debug feature")
set (TRALLOC_DEBUG_LOG       true CACHE BOOL "log in debug feature")

set (TRALLOC_SHARED   true CACHE BOOL "build shared libs")
set (TRALLOC_STATIC   true CACHE BOOL "build static libs")
set (TRALLOC_MAN      true CACHE BOOL "create man")
set (TRALLOC_TEST     true CACHE BOOL "build tests")
set (TRALLOC_EXAMPLES true CACHE BOOL "build examples")


if (NOT DEFINED CMAKE_INSTALL_LIBDIR)
    set (CMAKE_INSTALL_LIBDIR "lib" CACHE PATH "Output directory for libraries")
endif ()
if (NOT DEFINED CMAKE_INSTALL_INCLUDEDIR)
  set (CMAKE_INSTALL_INCLUDEDIR "include" CACHE PATH "Output directory for include files")
endif ()
if (NOT DEFINED CMAKE_INSTALL_MANDIR)
  set (CMAKE_INSTALL_MANDIR "share/man/man3" CACHE PATH "Output directory for include files")
endif ()


if (NOT DEFINED TRALLOC_TARGET)
    set (TRALLOC_TARGET ${PROJECT_NAME})
endif ()

set (CMAKE_MODULE_PATH "${PROJECT_SOURCE_DIR}/cmake/checks" "${PROJECT_SOURCE_DIR}/cmake/finds")
include (CheckBool)
check_bool ()
include (CheckFile)
check_file ()
include (CheckC99_inline)
check_c99_inline ()
include (CheckLTO)
check_lto ()
include (CheckPthread)
check_pthread ()

if (${TRALLOC_DESTRUCTOR} AND ${TRALLOC_HAVE_FILE})
    set (TRALLOC_FILE true)
endif ()
if (NOT ${TRALLOC_HAVE_PTHREAD} AND ${TRALLOC_THREADS})
    set (TRALLOC_THREADS false)
    message(WARNING "TRALLOC_THREADS is disabled, because you have no pthread.")
endif ()
if (NOT ${TRALLOC_THREADS} AND ${TRALLOC_DEBUG_THREADS})
    set (TRALLOC_DEBUG_THREADS false)
    message(WARNING "TRALLOC_DEBUG_THREADS is disabled, because TRALLOC_THREADS is disabled.")
endif ()

if (${TRALLOC_UTILS_BUFFER})
    set (TRALLOC_UTILS true)
endif ()
if (${TRALLOC_LENGTH} OR ${TRALLOC_DESTRUCTOR} OR ${TRALLOC_REFERENCE} OR ${TRALLOC_POOL} OR ${TRALLOC_THREADS})
    set (TRALLOC_EXTENSIONS true)
endif ()
if (${TRALLOC_DEBUG_THREADS} OR ${TRALLOC_DEBUG_CALLBACKS} OR ${TRALLOC_DEBUG_STATS})
    set (TRALLOC_DEBUG true)
else ()
    set (TRALLOC_DEBUG false)
endif ()
if (NOT ${TRALLOC_DEBUG} AND ${TRALLOC_DEBUG_LOG})
    set (TRALLOC_DEBUG_LOG false)
    message(WARNING "TRALLOC_DEBUG_LOG is disabled, because TRALLOC_DEBUG is disabled.")
endif ()

if (NOT ${TRALLOC_STATIC} AND NOT ${TRALLOC_SHARED})
    message (FATAL_ERROR "Tralloc can not be compiled. Please enable building of shared or static libraries")
endif ()


if (NOT DEFINED CMAKE_BUILD_TYPE OR CMAKE_BUILD_TYPE STREQUAL "")
    set (CMAKE_BUILD_TYPE DEBUG)
endif ()

set (CMAKE_C_FLAGS "${CMAKE_C_FLAGS} -pedantic -Wall -Wextra ${C99_CFLAGS} ${PTHREAD_CFLAGS}")
set (CMAKE_LD_FLAGS "${CMAKE_LD_FLAGS} ${PTHREAD_LDLAGS}")
set (CMAKE_C_FLAGS_DEBUG    "-pipe -O0 -g")
set (CMAKE_C_FLAGS_RELEASE  "-pipe -O2 ${LTO_CFLAGS}")
set (CMAKE_LD_FLAGS_RELEASE ${LTO_LDLAGS})


add_subdirectory ("src")
include_directories ("src")

if (${TRALLOC_TEST} OR ${TRALLOC_EXAMPLES})
    enable_testing()
endif ()
if (${TRALLOC_TEST})
    add_subdirectory ("tests")
endif ()
if (${TRALLOC_EXAMPLES})
    add_subdirectory ("examples")
endif ()

if (${TRALLOC_MAN})
    add_subdirectory ("man")
endif ()


include (CPackConfig.cmake)
include (CPack)
