set (HEADERS "context.h" "config.h" "macro.h" "types.h")
set (SOURCES
    "${CMAKE_CURRENT_SOURCE_DIR}/context.c"
)

if (${TRALLOC_EXTENSIONS})
    set (HEADERS ${HEADERS} "extensions.h")
    set (SOURCES ${SOURCES}
        "${CMAKE_CURRENT_SOURCE_DIR}/extensions.c"
    )
endif ()

if (${TRALLOC_DEBUG})
    set (HEADERS ${HEADERS} "debug.h")
endif ()
if (${TRALLOC_LENGTH})
    set (HEADERS ${HEADERS} "length.h")
endif()
if (${TRALLOC_DESTRUCTORS})
    set (HEADERS ${HEADERS} "destructors.h")
endif()
if (${TRALLOC_REFERENCES})
    set (HEADERS ${HEADERS} "references.h")
endif ()

install (FILES ${HEADERS} DESTINATION "${CMAKE_INSTALL_INCLUDEDIR}/${CURRENT_INCLUDEDIR}")

set (INCLUDEDIR ${CURRENT_INCLUDEDIR})

set (CURRENT_INCLUDEDIR "${INCLUDEDIR}/tree")
add_subdirectory ("tree")

if (${TRALLOC_DEBUG})
    set (CURRENT_INCLUDEDIR "${INCLUDEDIR}/debug")
    add_subdirectory ("debug")
endif ()
if (${TRALLOC_LENGTH})
    set (CURRENT_INCLUDEDIR "${INCLUDEDIR}/length")
    add_subdirectory ("length")
endif()
if (${TRALLOC_DESTRUCTORS})
    set (CURRENT_INCLUDEDIR "${INCLUDEDIR}/destructors")
    add_subdirectory ("destructors")
endif()
if (${TRALLOC_REFERENCES})
    set (CURRENT_INCLUDEDIR "${INCLUDEDIR}/references")
    add_subdirectory ("references")
endif ()
if (${TRALLOC_POOL})
    set (CURRENT_INCLUDEDIR "${INCLUDEDIR}/pool")
    add_subdirectory ("pool")
endif ()

set (CURRENT_INCLUDEDIR "${INCLUDEDIR}/helpers")
add_subdirectory ("helpers")

if (${TRALLOC_UTILS})
    set (CURRENT_INCLUDEDIR "${INCLUDEDIR}/utils")
    add_subdirectory ("utils")
endif ()

configure_file ("${CMAKE_CURRENT_SOURCE_DIR}/config.h.in" "${CMAKE_CURRENT_SOURCE_DIR}/config.h")

if (${TRALLOC_COLLECT_SOURCES})
    set (SOURCES ${CMAKE_CURRENT_SOURCE_DIR}/collect.c ${SOURCES})
    join ("${SOURCES}" " " FILES)
    set (SOURCES "${CMAKE_CURRENT_BINARY_DIR}/sources.c")
    execute_process (COMMAND sh -c "cat ${FILES} > ${SOURCES}")
endif ()

if (${TRALLOC_SHARED})
    add_library (${TRALLOC_TARGET} SHARED ${SOURCES})
    install (TARGETS ${TRALLOC_TARGET} DESTINATION ${CMAKE_INSTALL_LIBDIR})
    install (CODE "execute_process (COMMAND ${CMAKE_STRIP} --strip-unneeded -R .comment -R .GCC.command.line -R .note.gnu.gold-version \$ENV{DESTDIR}\${CMAKE_INSTALL_PREFIX}/${CMAKE_INSTALL_LIBDIR}/lib${TRALLOC_TARGET}.so)")
endif ()
if (${TRALLOC_STATIC})
    add_library (${TRALLOC_TARGET}-static STATIC ${SOURCES})
    set_target_properties (${TRALLOC_TARGET}-static PROPERTIES OUTPUT_NAME ${TRALLOC_TARGET})
    install (TARGETS ${TRALLOC_TARGET}-static DESTINATION ${CMAKE_INSTALL_LIBDIR})
    install (CODE "execute_process (COMMAND ${CMAKE_STRIP} --strip-unneeded -R .comment -R .GCC.command.line -R .note.gnu.gold-version \$ENV{DESTDIR}\${CMAKE_INSTALL_PREFIX}/${CMAKE_INSTALL_LIBDIR}/lib${TRALLOC_TARGET}.a)")
endif ()

if (${TRALLOC_TESTS})
    add_subdirectory ("tests")
endif ()
