configure_file (${CMAKE_CURRENT_SOURCE_DIR}/config.h.in ${CMAKE_CURRENT_SOURCE_DIR}/config.h)

install (FILES common.h config.h destructor.h length.h macro.h reference.h tree.h types.h DESTINATION "${CMAKE_INSTALL_INCLUDEDIR}/${CURRENT_INCLUDEDIR}")
set (SOURCES common.c)

if (${TRALLOC_DEBUG})
    install (FILES events.h DESTINATION "${CMAKE_INSTALL_INCLUDEDIR}/${CURRENT_INCLUDEDIR}")
    set (SOURCES ${SOURCES} events.c)
endif ()

set (INCLUDEDIR ${CURRENT_INCLUDEDIR})

set (CURRENT_INCLUDEDIR "${INCLUDEDIR}/tree")
add_subdirectory ("tree")

if (${TRALLOC_LENGTH})
    set (CURRENT_INCLUDEDIR "${INCLUDEDIR}/length")
    add_subdirectory ("length")
endif()
if (${TRALLOC_DESTRUCTOR})
    set (CURRENT_INCLUDEDIR "${INCLUDEDIR}/destructor")
    add_subdirectory ("destructor")
endif()
if (${TRALLOC_REFERENCE})
    set (CURRENT_INCLUDEDIR "${INCLUDEDIR}/reference")
    add_subdirectory ("reference")
endif ()
if (${TRALLOC_POOL})
    set (CURRENT_INCLUDEDIR "${INCLUDEDIR}/pool")
    add_subdirectory ("pool")
endif ()

set (CURRENT_INCLUDEDIR "${INCLUDEDIR}/helpers")
add_subdirectory ("helpers")

if (${TRALLOC_UTILS})
    set (CURRENT_INCLUDEDIR "${INCLUDEDIR}/utils")
    add_subdirectory ("utils")
endif ()

if (${TRALLOC_SHARED})
    add_library (${TRALLOC_TARGET} SHARED ${SOURCES})
    install (TARGETS ${TRALLOC_TARGET} DESTINATION ${CMAKE_INSTALL_LIBDIR})
    install (CODE "EXECUTE_PROCESS(COMMAND ${CMAKE_STRIP} --strip-unneeded -R .comment -R .GCC.command.line -R .note.gnu.gold-version \$ENV{DESTDIR}\${CMAKE_INSTALL_PREFIX}/${CMAKE_INSTALL_LIBDIR}/lib${TRALLOC_TARGET}.so)")
endif ()
if (${TRALLOC_STATIC})
    add_library (${TRALLOC_TARGET}-static STATIC ${SOURCES})
    set_target_properties (${TRALLOC_TARGET}-static PROPERTIES OUTPUT_NAME ${TRALLOC_TARGET})
    install (TARGETS ${TRALLOC_TARGET}-static DESTINATION ${CMAKE_INSTALL_LIBDIR})
    install (CODE "EXECUTE_PROCESS(COMMAND ${CMAKE_STRIP} --strip-unneeded -R .comment -R .GCC.command.line -R .note.gnu.gold-version \$ENV{DESTDIR}\${CMAKE_INSTALL_PREFIX}/${CMAKE_INSTALL_LIBDIR}/lib${TRALLOC_TARGET}.a)")
endif ()
